buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.18.0"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"
    }
}

ext {
    concurrencyLimitsVersion = "0.1.14"

    retrofitVersion = "2.5.0"
    okhttpVersion = "3.13.1"

    rxjavaVersion = "2.2.7"

    springBootVersion = "2.1.6.RELEASE"

    flexyPoolVersion = "2.1.1"

    resilience4jVersion = "0.13.2"

    slf4jVersion = "1.7.25"

    junitJupiterVersion = "5.4.0"
    mockitoVersion = "2.24.0"
}

subprojects {
    group = "com.github.wreulicke.bricks"
    version = "0.0.1"

    apply plugin: 'java'
    apply plugin: "jacoco"
    apply plugin: "maven-publish"
    apply plugin: "com.diffplug.gradle.spotless"

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    targetCompatibility = sourceCompatibility = "1.8"

    dependencies {
        testImplementation group: "org.junit.jupiter", name: "junit-jupiter", version: junitJupiterVersion
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockitoVersion

        testImplementation("org.springframework.boot:spring-boot-starter-test:$springBootVersion") {
            exclude module: "junit"
        }
        testImplementation "org.springframework.boot:spring-boot-starter-logging:$springBootVersion"
    }

    repositories {
        jcenter()
    }

    test {
        useJUnitPlatform()
    }


    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    spotless {
        def headerFile = "/** " + rootProject.file("LICENSE.md").text + "*/"
        java {
            licenseHeader headerFile, "(package|import)"
            eclipse().configFile rootProject.file("eclipse-format-setting.xml")
            trimTrailingWhitespace()
            endWithNewline()
            removeUnusedImports()
        }

        format 'misc', {
            target '*.gradle'

            indentWithSpaces(2)
            endWithNewline()
        }
    }
}
